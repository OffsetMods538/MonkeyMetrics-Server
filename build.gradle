plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.0'
}

final String versionSuffix = System.getenv("VERSION_SUFFIX")
version = "1.1.0${versionSuffix != null && !versionSuffix.isEmpty() ? "+${versionSuffix}" : ""}"
println "Version: ${version}"
group = 'top.offsetmonkey538.monkeymetrics'

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.netty:netty-all:${project.netty_version}" // TODO: probably only include alll that I need
    implementation "io.prometheus:prometheus-metrics-core:${project.prometheus_version}"
    implementation "io.prometheus:prometheus-metrics-exporter-httpserver:${project.prometheus_version}"
    implementation "com.google.code.gson:gson:${project.gson_version}"
    compileOnly "org.jetbrains:annotations:26.0.2"
}

jar {
    manifest {
        attributes "Main-Class": "top.offsetmonkey538.monkeymetrics.Main"
    }
}

tasks.register("copyJarForDocker", Copy) {
    dependsOn tasks.shadowJar

    from(tasks.shadowJar.outputs)
    into(layout.buildDirectory.dir("docker"))
    rename { "monkeymetrics.jar" }
}
